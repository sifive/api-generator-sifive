tuple VCSDUTSimulatorPlan =
  global TestDriver:     String
  global Waves:          Boolean
  global Coverage:       Boolean
  global Score:          DUT => DUTSimCompileOptions => DUTSimExecuteOptions => UserSimulationOptions => Double
  global CompileOptions: DUT => PrivateVCSDUTSimCompileOptions
  global ExecuteOptions: DUT => PrivateVCSDUTSimExecuteOptions

global def makeVCSDUTSimulatorPlan testDriver waves score compileOptions executeOptions =
  def coverage = False
  VCSDUTSimulatorPlan testDriver waves coverage score compileOptions executeOptions


tuple PrivateVCSDUTSimCompileOptions =
  global DUTOpts:   DUTSimCompileOptions
  global ExtraArgs: List NamedArg
  global LibraryDirs: List String # Verilog library include directories. These should be relative to the workspace root.

global def makePrivateVCSDUTSimCompileOptions =
  def dutOpts =
    emptyDUTSimCompileOptions
    | setDUTSimCompileOptionsResources defaultVCSResources
  PrivateVCSDUTSimCompileOptions dutOpts Nil Nil

def toVCSSimCompilePlan privateOpts outputDir coverage =
  def dutOpts      = privateOpts.getPrivateVCSDUTSimCompileOptionsDUTOpts

  def resources    = dutOpts.getDUTSimCompileOptionsResources
  def includeDirs  = dutOpts.getDUTSimCompileOptionsIncludeDirs
  def libraryDirs  = dutOpts.getDUTSimCompileOptionsLibraryDirs
  def defines      = dutOpts.getDUTSimCompileOptionsDefines
  def sourceFiles  = dutOpts.getDUTSimCompileOptionsSourceFiles
  def plusargs     = dutOpts.getDUTSimCompileOptionsPlusargs
  def extraArgs    = privateOpts.getPrivateVCSDUTSimCompileOptionsExtraArgs
  def extraLibraryDirs = privateOpts.getPrivateVCSDUTSimCompileOptionsLibraryDirs
  def withCoverage coverageEnabled plan =
    def editFn oldMetrics =
      if coverageEnabled then defaultCoverageMetrics else oldMetrics
    editVCSCompilePlanCoverageMetrics editFn plan

  makeVCSCompilePlan sourceFiles outputDir
  | setVCSCompilePlanResources   resources
  | setVCSCompilePlanExtraArgs   extraArgs
  | setVCSCompilePlanIncludeDirs includeDirs
  | setVCSCompilePlanLibraryDirs (libraryDirs ++ extraLibraryDirs)
  | setVCSCompilePlanDefines     defines
  | setVCSCompilePlanPlusargs    plusargs
  | withCoverage                 coverage

tuple PrivateVCSDUTSimExecuteOptions =
  global DUTOpts:   DUTSimExecuteOptions
  global ExtraArgs: List NamedArg

global def makePrivateVCSDUTSimExecuteOptions =
   PrivateVCSDUTSimExecuteOptions emptyDUTSimExecuteOptions Nil

def toVCSSimExecutePlan opts compileOutputs outputDir coverageOpts =
  def genericOpts = opts.getPrivateVCSDUTSimExecuteOptionsDUTOpts

  def plusargs     = genericOpts.getDUTSimExecuteOptionsPlusargs
  def visibleFiles = genericOpts.getDUTSimExecuteOptionsVisibleFiles
  def resources    = genericOpts.getDUTSimExecuteOptionsResources
  def extraArgs    = opts.getPrivateVCSDUTSimExecuteOptionsExtraArgs
  def withCoverageOpts coverageOpts plan =
    match coverageOpts
      None = plan
      Some (Pair maybeDir maybeName) =
        plan
        | setVCSExecutePlanCoverageMetrics defaultCoverageMetrics
        | editVCSExecutePlanCoverageDir (maybeDir | orElse _)
        | editVCSExecutePlanCoverageTestName (maybeName | orElse _)

  makeVCSExecutePlan compileOutputs outputDir
  | setVCSExecutePlanPlusargs     plusargs
  | setVCSExecutePlanVisibleFiles visibleFiles
  | setVCSExecutePlanExtraArgs    extraArgs
  | setVCSExecutePlanResources    resources
  | withCoverageOpts coverageOpts

global topic vcsDUTSimCompileOptionsHooks : DUT => Option (PrivateVCSDUTSimCompileOptions => PrivateVCSDUTSimCompileOptions)
global topic vcsDUTSimExecuteOptionsHooks : DUT => Option (PrivateVCSDUTSimExecuteOptions => PrivateVCSDUTSimExecuteOptions)

# takes a VCSDUTSimulatorPlan and makes a DUTSimulator
global def makeVCSDUTSimulator plan =
  def simName   = "vcs"
  def simVendor = "synopsys"
  def testDriver = plan.getVCSDUTSimulatorPlanTestDriver
  def waves  = plan.getVCSDUTSimulatorPlanWaves
  def coverage = plan.getVCSDUTSimulatorPlanCoverage
  def score  = plan.getVCSDUTSimulatorPlanScore

  def imp dut rawCompileOptions rawExecuteOptions userOptions =
    def compileOutputs =
      def appendUserPlusargs = editDUTSimCompileOptionsPlusargs (_ ++ userOptions.getUserSimulationOptionsCompilePlusargs)
      def appendDUTOptions = appendDUTSimCompileOptions rawCompileOptions
      def privateOpts =
        plan.getVCSDUTSimulatorPlanCompileOptions dut
        | editPrivateVCSDUTSimCompileOptionsDUTOpts appendDUTOptions
        | editPrivateVCSDUTSimCompileOptionsDUTOpts appendUserPlusargs
        | applyPartialEdits dut (subscribe vcsDUTSimCompileOptionsHooks)
      def outputDir = userOptions.getUserSimulationOptionsCompileOutputDir
      toVCSSimCompilePlan privateOpts outputDir coverage
      | doVCSCompile

    def executeOutputs =
      def appendUserPlusargs = editDUTSimExecuteOptionsPlusargs (_ ++ userOptions.getUserSimulationOptionsExecutePlusargs)
      def appendDUTOptions = appendDUTSimExecuteOptions rawExecuteOptions
      def privateOpts =
        plan.getVCSDUTSimulatorPlanExecuteOptions dut
        | editPrivateVCSDUTSimExecuteOptionsDUTOpts appendDUTOptions
        | editPrivateVCSDUTSimExecuteOptionsDUTOpts appendUserPlusargs
        | applyPartialEdits dut (subscribe vcsDUTSimExecuteOptionsHooks)
      def outputDir = userOptions.getUserSimulationOptionsExecuteOutputDir
      def covDir = userOptions.getUserSimulationOptionsCoverageDir
      def covName = userOptions.getUserSimulationOptionsCoverageTestName
      def coverageOpts =
        if coverage then
          Some (Pair covDir covName)
        else
          None
      toVCSSimExecutePlan privateOpts compileOutputs outputDir coverageOpts
      | doVCSExecute

    executeOutputs
    | getVCSExecuteOutputsJob
    | makeSimulationOutput

  makeDUTSimulatorPlan simName simVendor testDriver waves score imp
  | setDUTSimulatorPlanCoverage coverage
  | makeDUTSimulator


def vcsCompileOptsExtract waves dut =

  def extraArgs =
    NamedArg "-full64",             NamedArg "-notice",      NamedArg "-error=VCM-HFUFR",
    NamedArg "-error=LFCOR",        NamedArg "-error=CFCOR", NamedArg "-error=BMII",
    NamedArg "-timescale=1ns/10ps", NamedArg "-quiet",       NamedArg "+vcs+lic+wait",
    NamedArg "-Mupdate",            NamedArg "+vc+list",     NamedArg "-sverilog",
    NamedArg "-ntb_opts",           NamedArg "uvm-1.2",      NamedArg "-j4",
    NamedArg "+rad",                NamedArg "-hsopt=gates", NamedArg "-O3",
    NamedArg "-reportstats",        NamedArg "-assert",      NamedArg "svaext",
    NamedArg "-LDFLAGS -lrt",       Nil

  def withWavesOptions opts =
    if waves
    then
      def withExtraArgs =
        NamedArg "-kdb",
        NamedArg "-lca",
        NamedArg "-debug_access+r+line+class+driver",
        NamedArg "-LDFLAGS",
        NamedArg "-fno-lto",
        _
      def withDefines =
        NamedArg "SVT_FSDB_ENABLE",
        NamedArg "DEBUG",
        _
      opts
      | editPrivateVCSDUTSimCompileOptionsExtraArgs withExtraArgs
      | editPrivateVCSDUTSimCompileOptionsDUTOpts (editDUTSimCompileOptionsDefines withDefines)
    else opts

  def genericOpts =
    freedomTestDriverSimCompileOpts dut
    | editDUTSimCompileOptionsDefines (NamedArg "VCS", _)

  makePrivateVCSDUTSimCompileOptions
  | editPrivateVCSDUTSimCompileOptionsDUTOpts genericOpts.appendDUTSimCompileOptions
  | setPrivateVCSDUTSimCompileOptionsExtraArgs extraArgs
  | withWavesOptions

def vcsExecuteOptsExtract waves dut =
  def withExtraArgs =
    NamedArgString "-assert" "old_ctrl_msg",
    NamedArgString "-assert" "nopostproc",
    NamedArgString "-assert" "global_finish_maxfail=10",
    NamedArg "-error=STASKW_CO1",
    NamedArg "-error=STASKW_RMCOF",
    NamedArg "-error=STASKW_RMIEAFL",
    NamedArg "-error=PHNE",
    _

  def withWavesOptions opts =
    if waves
    then
      editPrivateVCSDUTSimExecuteOptionsDUTOpts (editDUTSimExecuteOptionsPlusargs (NamedArgString "vcdplusfile" "sim.vpd", _)) opts
    else opts

  def genericOpts = freedomTestDriverSimExecuteOpts dut

  makePrivateVCSDUTSimExecuteOptions
  | editPrivateVCSDUTSimExecuteOptionsDUTOpts genericOpts.appendDUTSimExecuteOptions
  | editPrivateVCSDUTSimExecuteOptionsExtraArgs withExtraArgs
  | withWavesOptions

def defaultCoverageMetrics =
  makeVCSCoverageMetrics
  | setVCSCoverageMetricsLine True
  | setVCSCoverageMetricsTgl True
  | setVCSCoverageMetricsBranch True
  | setVCSCoverageMetricsAssert True

def makeFreedomVCSDUTSimulator waves coverage =
  def name = "vcs"
  def score = (\_ \_ \_ \_ 1.0) # TODO
  def compileOptions = vcsCompileOptsExtract waves
  def executeOptions = vcsExecuteOptsExtract waves
  makeVCSDUTSimulatorPlan name waves score compileOptions executeOptions
  | setVCSDUTSimulatorPlanCoverage coverage
  | makeVCSDUTSimulator


publish dutSimulators =
  freedomVCSDUTSimulator,
  freedomVCSWavesDUTSimulator,
  freedomVCSCoverageDUTSimulator,
  freedomVCSWavesCoverageDUTSimulator,
  Nil

global def freedomVCSDUTSimulator = makeFreedomVCSDUTSimulator False False
global def freedomVCSWavesDUTSimulator = makeFreedomVCSDUTSimulator True False
global def freedomVCSCoverageDUTSimulator = makeFreedomVCSDUTSimulator False True
global def freedomVCSWavesCoverageDUTSimulator = makeFreedomVCSDUTSimulator True True
